# CUDA NDT Matcher - Build System

# Cargo config generated by colcon-cargo-ros2
cargo_config := "build/ros2_cargo_config.toml"
manifest := "src/Cargo.toml"

# Paths for testing
autoware_setup := "external/autoware_repo/install/setup.bash"
local_setup := "install/setup.bash"
sample_map_dir := "data/sample-map-rosbag"
sample_rosbag := "data/sample-rosbag"

# Show available recipes
default:
    @just --list

# Build all packages with colcon
build:
    #!/usr/bin/env bash
    source {{autoware_setup}}
    colcon build --symlink-install --cmake-args -DCMAKE_BUILD_TYPE=Release --cargo-args --release

# Clean all build artifacts
clean:
    rm -rf build install log src/target

# Format code with rustfmt
format:
    cargo +nightly fmt --manifest-path {{manifest}}

# Check formatting and run clippy
lint:
    #!/usr/bin/env bash
    source {{local_setup}}
    cargo +nightly fmt --check --manifest-path {{manifest}}
    cargo clippy --manifest-path {{manifest}} --config {{cargo_config}} --all-targets

# Run all tests
test:
    #!/usr/bin/env bash
    source {{local_setup}}
    cargo test --manifest-path {{manifest}} --config {{cargo_config}} --all-targets

# Run all quality checks (lint + test)
quality: lint test

# Download sample map and rosbag data
download-data:
    ./scripts/download_sample_data.sh

# Service unit names
ndt_autoware_unit := "ndt-autoware"
ndt_cuda_unit := "ndt-cuda"
rosbag_unit := "rosbag-player"

# Start Autoware NDT as a systemd user service
start-ndt-autoware:
    #!/usr/bin/env bash
    set -eo pipefail
    systemctl --user stop {{ndt_autoware_unit}} 2>/dev/null || true
    systemctl --user reset-failed {{ndt_autoware_unit}} 2>/dev/null || true
    systemd-run --user --unit={{ndt_autoware_unit}} --same-dir --collect \
        --setenv=DISPLAY="$DISPLAY" \
        scripts/run_ndt_simulation.sh "$(realpath {{sample_map_dir}})"
    echo "Started {{ndt_autoware_unit}} service. Use 'just stop-ndt-autoware' to stop."

# Stop Autoware NDT service
stop-ndt-autoware:
    systemctl --user stop {{ndt_autoware_unit}} || true
    @echo "Stopped {{ndt_autoware_unit}} service."

# Restart Autoware NDT service
restart-ndt-autoware: stop-ndt-autoware start-ndt-autoware

# Show Autoware NDT service status
status-ndt-autoware:
    systemctl --user status {{ndt_autoware_unit}} || true

# Show Autoware NDT logs
log-ndt-autoware:
    journalctl --user -u {{ndt_autoware_unit}} -f

# Start CUDA NDT as a systemd user service
start-ndt-cuda:
    #!/usr/bin/env bash
    set -eo pipefail
    systemctl --user stop {{ndt_cuda_unit}} 2>/dev/null || true
    systemctl --user reset-failed {{ndt_cuda_unit}} 2>/dev/null || true
    systemd-run --user --unit={{ndt_cuda_unit}} --same-dir --collect \
        --setenv=DISPLAY="$DISPLAY" \
        scripts/run_ndt_simulation.sh --cuda "$(realpath {{sample_map_dir}})"
    echo "Started {{ndt_cuda_unit}} service. Use 'just stop-ndt-cuda' to stop."

# Stop CUDA NDT service
stop-ndt-cuda:
    systemctl --user stop {{ndt_cuda_unit}} || true
    @echo "Stopped {{ndt_cuda_unit}} service."

# Restart CUDA NDT service
restart-ndt-cuda: stop-ndt-cuda start-ndt-cuda

# Show CUDA NDT service status
status-ndt-cuda:
    systemctl --user status {{ndt_cuda_unit}} || true

# Show CUDA NDT logs
log-ndt-cuda:
    journalctl --user -u {{ndt_cuda_unit}} -f

# Start rosbag player as a systemd user service
start-rosbag:
    #!/usr/bin/env bash
    set -eo pipefail
    systemctl --user stop {{rosbag_unit}} 2>/dev/null || true
    systemctl --user reset-failed {{rosbag_unit}} 2>/dev/null || true
    systemd-run --user --unit={{rosbag_unit}} --same-dir --collect \
        scripts/play_rosbag.sh "$(realpath {{sample_rosbag}})"
    echo "Started {{rosbag_unit}} service. Use 'just stop-rosbag' to stop."

# Stop rosbag player service
stop-rosbag:
    systemctl --user stop {{rosbag_unit}} || true
    @echo "Stopped {{rosbag_unit}} service."

# Restart rosbag player service
restart-rosbag: stop-rosbag start-rosbag

# Show rosbag player service status
status-rosbag:
    systemctl --user status {{rosbag_unit}} || true

# Show rosbag player logs
log-rosbag:
    journalctl --user -u {{rosbag_unit}} -f

# Start Autoware demo (NDT + rosbag)
start-demo-autoware: start-ndt-autoware
    @sleep 30
    @echo "Starting rosbag playback..."
    just start-rosbag

# Start CUDA demo (NDT + rosbag)
start-demo-cuda: start-ndt-cuda
    @sleep 30
    @echo "Starting rosbag playback..."
    just start-rosbag

# Stop all demo services
stop-demo:
    just stop-rosbag
    just stop-ndt-cuda
    just stop-ndt-autoware

# Enable NDT matching via service call
enable-ndt:
    #!/usr/bin/env bash
    source {{autoware_setup}}
    ros2 service call /localization/pose_estimator/trigger_node_srv std_srvs/srv/SetBool "{data: true}"

# Monitor NDT pose output
monitor-ndt:
    #!/usr/bin/env bash
    source {{autoware_setup}}
    ros2 topic echo /localization/pose_estimator/pose_with_covariance

# CUDA NDT Matcher - Build System

# Cargo config generated by colcon-cargo-ros2
cargo_config := "build/ros2_cargo_config.toml"
manifest := "Cargo.toml"

# Paths for testing
autoware_setup := "external/autoware_repo/install/setup.bash"
local_setup := "install/setup.bash"
sample_map_dir := "data/sample-map-rosbag"
sample_rosbag := "data/sample-rosbag-fixed"

# Show available recipes
default:
    @just --list

# Build all packages with colcon (only check src/ directory)
build:
    #!/usr/bin/env bash
    source {{autoware_setup}}
    colcon build --base-paths src --symlink-install --cmake-args -DCMAKE_BUILD_TYPE=Release --cargo-args --release

# Clean all build artifacts
clean:
    rm -rf build install log target

# Format code with rustfmt
format:
    cargo +nightly fmt --manifest-path {{manifest}} --all

# Check formatting and run clippy
lint:
    #!/usr/bin/env bash
    source {{local_setup}}
    cargo clippy --manifest-path {{manifest}} --config {{cargo_config}} --all-targets
    cargo +nightly fmt --check --manifest-path {{manifest}} --all

# Run all tests
test:
    #!/usr/bin/env bash
    source {{local_setup}}
    cargo test --manifest-path {{manifest}} --config {{cargo_config}} --all-targets

# Run all quality checks (lint + test)
quality: lint test

# Download sample map and rosbag data
download-data:
    ./scripts/download_sample_data.sh

play-rosbag:
    #!/usr/bin/env bash
    set -eo pipefail
    source {{autoware_setup}}
    ros2 bag play -l $(realpath {{sample_rosbag}})

# Start Autoware demo (NDT + rosbag) - runs simulation and rosbag in parallel
run-builtin:
    #!/usr/bin/env bash
    set -euo pipefail
    parallel --halt now,done=1 --line-buffer ::: \
        "./scripts/run_ndt_simulation.sh '$(realpath {{sample_map_dir}})'" \
        "sleep 30 && source {{autoware_setup}} && ros2 bag play -l '$(realpath {{sample_rosbag}})'"

# Start CUDA demo (NDT + rosbag) - runs simulation and rosbag in parallel
run-cuda:
    #!/usr/bin/env bash
    set -euo pipefail
    parallel --halt now,done=1 --line-buffer ::: \
        "./scripts/run_ndt_simulation.sh --cuda '$(realpath {{sample_map_dir}})'" \
        "sleep 30 && source {{autoware_setup}} && ros2 bag play -l '$(realpath {{sample_rosbag}})'"

# Enable NDT matching via service call
enable-ndt:
    #!/usr/bin/env bash
    source {{autoware_setup}}
    ros2 service call /localization/pose_estimator/trigger_node_srv std_srvs/srv/SetBool "{data: true}"

# Monitor NDT pose output
monitor-ndt:
    #!/usr/bin/env bash
    source {{autoware_setup}}
    ros2 topic echo /localization/pose_estimator/pose_with_covariance

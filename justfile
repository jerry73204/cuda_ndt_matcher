# CUDA NDT Matcher - Build System

# Cargo config generated by colcon-cargo-ros2
cargo_config := "build/ros2_cargo_config.toml"
manifest := "Cargo.toml"

# Paths for testing
autoware_setup := "external/autoware_repo/install/setup.bash"
local_setup := "install/setup.bash"
sample_map_dir := "data/sample-map-rosbag"
sample_rosbag := "data/sample-rosbag-fixed"
rosbag_output_dir := "rosbag"
ground_truth_dir := "tests/fixtures/ground_truth"

# NDT output/debug topics to record
ndt_output_topics := "/localization/pose_estimator/pose /localization/pose_estimator/pose_with_covariance /localization/pose_estimator/ndt_marker /localization/pose_estimator/points_aligned /localization/pose_estimator/monte_carlo_initial_pose_marker /localization/pose_estimator/transform_probability /localization/pose_estimator/nearest_voxel_transformation_likelihood /localization/pose_estimator/iteration_num /localization/pose_estimator/exe_time_ms /localization/pose_estimator/initial_pose_with_covariance /localization/pose_estimator/initial_to_result_distance /localization/pose_estimator/initial_to_result_relative_pose"

# NDT input topics (for debugging)
ndt_input_topics := "/localization/pose_twist_fusion_filter/biased_pose_with_covariance /localization/util/downsample/pointcloud"

# All NDT topics (input + output)
ndt_topics := ndt_output_topics + " " + ndt_input_topics

# Show available recipes
default:
    @just --list

# Build all packages with colcon (only check src/ directory)
build:
    #!/usr/bin/env bash
    source {{autoware_setup}}
    colcon build --base-paths src --symlink-install --cmake-args -DCMAKE_BUILD_TYPE=Release --cargo-args --release

# Clean all build artifacts
clean:
    rm -rf build install log target

# Format code with rustfmt
format:
    cargo +nightly fmt --manifest-path {{manifest}} --all

# Check formatting and run clippy on all crates
lint:
    #!/usr/bin/env bash
    source {{local_setup}}
    cargo +nightly fmt --check --manifest-path {{manifest}} --all
    cargo clippy --manifest-path {{manifest}} --config {{cargo_config}} --all-targets

# Lint ndt_cuda crate only
lint-ndt-cuda:
    #!/usr/bin/env bash
    source {{local_setup}}
    cargo +nightly fmt --check --manifest-path {{manifest}} -p ndt_cuda
    cargo clippy --manifest-path {{manifest}} --config {{cargo_config}} -p ndt_cuda

# Lint cuda_ffi crate only
lint-cuda-ffi:
    #!/usr/bin/env bash
    source {{local_setup}}
    cargo +nightly fmt --check --manifest-path {{manifest}} -p cuda_ffi
    cargo clippy --manifest-path {{manifest}} --config {{cargo_config}} -p cuda_ffi

# Lint cuda_ndt_matcher crate only
lint-cuda-ndt-matcher:
    #!/usr/bin/env bash
    source {{local_setup}}
    cargo +nightly fmt --check --manifest-path {{manifest}} -p cuda_ndt_matcher
    cargo clippy --manifest-path {{manifest}} --config {{cargo_config}} -p cuda_ndt_matcher

# Run all Rust unit tests
test-rust:
    #!/usr/bin/env bash
    source {{local_setup}}
    # Note: --test-threads=1 required to avoid CUDA/PCL thread-safety issues in fast-gicp tests
    cargo test --manifest-path {{manifest}} --config {{cargo_config}} --all-targets -- --test-threads=1

# Test ndt_cuda crate only
test-ndt-cuda:
    #!/usr/bin/env bash
    source {{local_setup}}
    cargo test --manifest-path {{manifest}} --config {{cargo_config}} -p ndt_cuda -- --test-threads=1

# Test cuda_ffi crate only
test-cuda-ffi:
    #!/usr/bin/env bash
    source {{local_setup}}
    cargo test --manifest-path {{manifest}} --config {{cargo_config}} -p cuda_ffi

# Test cuda_ndt_matcher crate only
test-cuda-ndt-matcher:
    #!/usr/bin/env bash
    source {{local_setup}}
    cargo test --manifest-path {{manifest}} --config {{cargo_config}} -p cuda_ndt_matcher

# Run Rust unit tests only (fast)
test: test-rust

# Run all tests including integration tests (slow, requires sample data)
test-all: test-rust test-integration

# Run Python integration tests (prepares data if needed)
test-integration: ensure-test-data
    #!/usr/bin/env bash
    set -eo pipefail
    cd "$(dirname {{manifest}})"

    # Check if pytest is available
    if ! command -v pytest &> /dev/null; then
        echo "pytest not found. Install with: pip install -r tests/requirements.txt"
        exit 1
    fi

    # Run integration tests
    pytest tests/test_cuda_ndt.py -v

# Ensure test data is available (idempotent)
ensure-test-data: ensure-sample-data ensure-ground-truth

# Download sample data if not present (idempotent)
ensure-sample-data:
    #!/usr/bin/env bash
    set -eo pipefail
    if [[ -d "{{sample_map_dir}}" && -d "{{sample_rosbag}}" ]]; then
        echo "Sample data already exists, skipping download"
    else
        echo "Downloading sample data..."
        ./scripts/download_sample_data.sh
    fi

# Collect ground truth if not present (idempotent)
ensure-ground-truth: ensure-sample-data
    #!/usr/bin/env bash
    set -eo pipefail
    if [[ -d "{{ground_truth_dir}}/rosbag" ]]; then
        echo "Ground truth already exists, skipping collection"
    else
        echo "Collecting ground truth data..."
        ./scripts/collect_ground_truth.sh
    fi

# Force re-collection of ground truth
refresh-ground-truth: ensure-sample-data
    #!/usr/bin/env bash
    set -eo pipefail
    echo "Refreshing ground truth data..."
    rm -rf "{{ground_truth_dir}}/rosbag" "{{ground_truth_dir}}/debug.jsonl" "{{ground_truth_dir}}/metadata.json"
    ./scripts/collect_ground_truth.sh

# Run all quality checks (lint + test)
quality: lint test

# Download sample map and rosbag data
download-data:
    ./scripts/download_sample_data.sh

play-rosbag:
    #!/usr/bin/env bash
    set -eo pipefail
    source {{autoware_setup}}
    ros2 bag play -l $(realpath {{sample_rosbag}})

# Start Autoware builtin NDT demo (simulation + rosbag + recording)
run-builtin:
    ./scripts/run_demo.sh "$(realpath {{sample_map_dir}})" "$(realpath {{sample_rosbag}})" "{{rosbag_output_dir}}" {{ndt_topics}}

# Start CUDA NDT demo (simulation + rosbag + recording)
run-cuda:
    ./scripts/run_demo.sh --cuda "$(realpath {{sample_map_dir}})" "$(realpath {{sample_rosbag}})" "{{rosbag_output_dir}}" {{ndt_topics}}

# Enable NDT matching via service call
enable-ndt:
    #!/usr/bin/env bash
    source {{autoware_setup}}
    ros2 service call \
        /localization/pose_estimator/trigger_node_srv \
        std_srvs/srv/SetBool \
        "{data: true}"

# Monitor NDT pose output
monitor-ndt:
    #!/usr/bin/env bash
    source {{autoware_setup}}
    ros2 topic echo /localization/pose_estimator/pose_with_covariance
